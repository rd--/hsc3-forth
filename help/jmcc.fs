\ ANALOG BUBBLES (JMCC) #1

8 7.23 2 MCE 0 LFSAW.KR 3 * 80 + ( OFFSET )
0.4 0 LFSAW.KR 24 * + MIDICPS ( FREQ )
0 SINOSC.AR 0.04 * 0.2 0.2 4 COMBN
PLAY

\ LFO MODULATION (JMCC) #1

0.6 0.7 2 MCE 0 FSINOSC.KR 3600 * 4000 + ( P )
0.05 0 FSINOSC.KR 80 * 160 + ( O )
0 0.4 LFPULSE.AR 0.05 * SWAP 0.2 RLPF ( S )
0.3 0.2 0.25 2 MCE 2 COMBL
PLAY

\ HELL IS BUSY (JMCC) #1

400 0 2000 RAND.IR + 0 FSINOSC.AR 1 0 10 RAND.IR + 0 0 0.7 RAND.IR LFPULSE.KR 0.04 * *
-1 1 RAND.IR 1 PAN2
4 4 8 INF OVERLAP-TEXTURE

\ POND LIFE (JMCC) #1

20 0 30 RAND.IR + 0 FSINOSC.KR 100 400 RAND.IR * 500 2500 0 LINRAND.IR + 0 SINOSC.AR
3 1 9 RAND.IR / 0 0.2 0.5 RAND.IR LFPULSE.KR 0.04 * *
-1 1 RAND.IR 0.5 PAN2
8 8 4 INF OVERLAP-TEXTURE

\ ALIEN FROGGIES (JMCC) #1

: ALIEN-FROGGIE
    11 -0.2 0.2 0 LINRAND.IR EXP * 1 30 FOLD { R }
    R 200 3000 EXPRAND 0 9 RAND.IR R * R + FORMANT.AR 0.05 *
;
: ALIEN-FROGGIES ALIEN-FROGGIE 0.25 0.5 5 INF OVERLAP-TEXTURE ;
ALIEN-FROGGIES PLAY

\ RANDOM SINE WAVES (JMCC) #1

0 2000 RAND.IR 0 FSINOSC.AR 0.02 * -1 1 RAND.IR 1 PAN2
2 5 12 INF OVERLAP-TEXTURE

\ RANDOM PULSATIONS (JMCC) #1

0 2000 RAND.IR 0 FSINOSC.AR 1 1 0 1 0 2 5 2 0.02 ENV-LINEN ENVGEN.KR *
8 88 0 LINRAND.IR 0 SINOSC.AR AMCLIP 0.3 0.8 RAND.IR 0 6.28319 RAND.IR SINOSC.KR 0.7 * 1 PAN2
9 8 / INF SPAWN-TEXTURE

\ MOTO REV (JMCC) #1

0.2 0 SINOSC.KR 10 * 21 + ( FREQ )
0 0.1 2 MCE 0.1 LFPULSE.AR ( OSC )
100 0.1 RLPF 0.4 SWAP CLIP2
PLAY

\ SCRATCHY (JMCC) #1

0.49 BROWNNOISE.AR 0.5 * SWAP -
2 CLONE
0 MAX 20 * 5000 1 RHPF
PLAY

\ TREMULATE (JMCC) #1

: O 0 400 RAND.IR 500 + 1.0 1.2 1.5 1.8 4 MCE * 0 FSINOSC.AR ;
: A 0 30 RAND.IR 30 + 4 CLONE LFNOISE2.KR 0.1 * 0 MAX ;
: L 0 1 RAND.IR 4 CLONE ;
: TREMULATE O L A PAN2 MIX ;
: TREMULATES TREMULATE 0.5 2 3 INF OVERLAP-TEXTURE ;
: CMB 0.1 0.1 1 COMBN ;
' CMB TEXTURE-POST-PROC
TREMULATES

\ RESO-PULSE (JMCC) #1

: RESO-PULSE
    25 30 34 37 41 42 46 49 53 54 58 61 63 66 14 MCE CHOOSE MIDICPS { F }
    F 0 0.2 LFPULSE.AR 2 F * -0.5 0.5 RAND.IR + 0 0.2 LFPULSE.AR + 0.02 *
;
: POST-PROC
    6 LFNOISE0.KR 1000 * 1200 + 0.2 0.02 1 0.2 MOUSEX.KR RLPF { S }
    S S 0.33333 0.33333 DELAYN 2 MCE
;
' POST-PROC TEXTURE-POST-PROC
RESO-PULSE 4 2 4 INF OVERLAP-TEXTURE

\ SPRINKLER (JMCC) #1

0.09 0 0.16 LFPULSE.KR 10 * 7 + ( FREQ )
0 0.25 LFPULSE.KR 0.1 * ( TRIG )
WHITENOISE.AR * BPZ2
PLAY

\ SPRINKLER MOUSE (JMCC) #1

0.2 50 LINEAR 0.2 MOUSEX.KR ( FREQ )
0 0.25 LFPULSE.KR 0.1 * ( TRIG )
WHITENOISE.AR * BPZ2
PLAY

\ HARMONIC SWIMMING (JMCC) #1

: A 0.02 ;
: F 50 ;
: P 20 ;
: L 0 A NEGATE 60 DO-NOTHING LINE.KR ;
: O { H }
    2 8 RAND.IR 2 CLONE LFNOISE1.KR { N }
    0 N A * L + MAX { E }
    F H 1 + * 0 FSINOSC.AR E * ;
: HS 0 P 0 DO I O + LOOP ;
HS PLAY

\ HARMONIC TUMBLING (JMCC) #1

: F 80 ;
: P 10 ;
: T 10 11 2 MCE 0.1 60 DO-NOTHING XLINE.KR ;
: O { H }
    T DUST.KR 0.02 * 0.005 0.25 0.5 RAND.IR DECAY2
    F H 1 + * 0 FSINOSC.AR * ;
: HT 0 P 0 DO I O + LOOP ;
HT PLAY

\ RAILS (JMCC) #2

: RAILS
    100 DUST.AR 0.04 * 3000 300 8 0 XLINE.KR 0.2 RESONZ
    1 0 1
    20 0 DO 200 0 3000 0 LINRAND.IR + 1 0.2 0 1 RAND.IR + LOOP 20 3 * MCE
    KLANK -1 1 RAND.IR -1 1 RAND.IR 8 0 LINE.KR 1 PAN2
;
RAILS 3 2 4 INF OVERLAP-TEXTURE

\ BOUNCING OBJECTS (JMCC) #2

: PARAM 4 0 DO 400 8400 RAND.IR 0 1 RAND.IR 0.01 0.11 RAND.IR LOOP 12 MCE ;
5 -2 2 RAND.IR + 600 4 DO-NOTHING XLINE.KR 0 IMPULSE.AR
0.09 EPSILON 4 DO-NOTHING XLINE.KR * 0.001 DECAY
1 0 1 PARAM KLANK.AR
-1 1 RAND.IR 1 PAN2.AR
1 1 0 1 REMOVE-SYNTH 1 2 -99 -99 1 3 1 0 0 0.001 1 0 12 MCE ENVGEN.KR *
0.6 0 0.6 RAND.IR DELAYN
0.6 INF SPAWN-TEXTURE

\ LOTS-O-SINS (JMCC) #2

: N 60 ;
: PARAM N 0 DO 0 10000 0 LINRAND.IR 40 + 1 1 LOOP N 3 * MCE ;
: LOTS-O-SINS 1 0 PARAM KLANG.AR 2 CLONE 0.1 * N RECIP * ;
LOTS-O-SINS 4 4 INF XFADE-TEXTURE

\ CLUSTERED SINES (JMCC) #2

: CS
    80 { N }
    100 1100 RAND.IR { F1 }
    4 F1 *  { F2 }
    N 0 DO 0 F2 RAND.IR F1 + { Y } Y F1 Y / 0 LOOP N 3 * MCE { PARAM }
    1 0 PARAM KLANG.AR 0.3 N / * 2 CLONE
;
CS 4 4 INF XFADE-TEXTURE

\ RESONATORS HARMONIC SERIES (JMCC) #2

: P 12 ;
: RAT 1 1.125 1.25 1.333 1.5 1.667 1.875 2 2.25 2.5 2.667 3 3.333 3.75 4 15 MCE ;
: PARAM { F } P 0 DO I F * F + -0.5 0.5 RAND.IR + 1 I 1 + / 0.5 4.5 RAND.IR LOOP P 3 * MCE ;
BROWNNOISE.AR 0.001 * 1 0 1 RAT CHOOSE 120 * PARAM KLANK.AR 2 CLONE
1 7 INF XFADE-TEXTURE

\ SWEPT RESONANT NOISE (JMCC) #2

: PARAM { P } P 0 DO 80 10080 0 LINRAND.IR 1 0.5 2.5 RAND.IR LOOP P 3 * MCE ;
WHITENOISE.AR 0.007 *
0.1 0.3 RAND.IR 0 FSINOSC.KR 0 24 RAND.IR * 36 84 RAND.IR + MIDICPS
0.1 RESONZ
1 0 1 10 PARAM KLANK 2 CLONE
4 4 5 INF OVERLAP-TEXTURE

\ COOLANT (JMCC) #2

: N 20 ;
: PARAM N 0 DO 0 2000 RAND.IR 40 + 1 1 LOOP N 3 * MCE ;
: SRC BROWNNOISE.AR 2 CLONE 0.0015 * 0.95 ONEPOLE ;
: COOLANT SRC 1 0 1 PARAM KLANK.AR 2 CLONE ;
: COOLANTS COOLANT 4 4 2 INF OVERLAP-TEXTURE ;
COOLANTS

\ PULSING BOTTLES (JMCC) #2

: R WHITENOISE.AR 4 14 RAND.IR 0 0.25 LFPULSE.KR *
    0 0.7 RAND.IR * 400 7400 RAND.IR 0.01 RESONZ ;
: S 0.1 0.5 RAND.IR 0 6.28319 RAND.IR SINOSC.KR ;
: PB 0 6 0 DO R S 1 PAN2 + LOOP ;
PB 4 4 4 INF OVERLAP-TEXTURE

\ WHAT WAS I THINKING? (JMCC) #2

: Z 4 0 SINOSC.KR 80 + 0.1 0 0.05 LFPULSE.AR 8 0 IMPULSE.AR
    * 500 * 2 DECAY MAX 0.157 LFNOISE1.KR 0.4 * 0.5 + PULSE.AR
    0.04 * 0.2 LFNOISE1.KR 2000 * 2400 + 0.2 RLPF ;
: C 0.6 * 0.06 0 0.3 RAND.IR LFNOISE1.KR 0.025 * 0.035 + 1 COMBL ;
: Y Z 0.6 * ;
Y C MIX Y C MIX 2 MCE Z + PLAY

\ NARROW BAND FILTERED CRACKLE NOISE (JMCC) #2

: NBFCN
    1.97 CRACKLE.AR 0 0.03 RAND.IR 0.15 * + 0 2000 RAND.IR 80 + { RF1 }
    RF1 RF1 -0.2 0.2 RAND.IR RF1 *
    + 9 0 XLINE.KR 0.2 RESONZ -1 1 RAND.IR 1 1 0 1 2 2 5 2 1 ENV-LINEN ENVGEN.AR PAN2
;
NBFCN 2 INF SPAWN-TEXTURE

\ RESONANT DUST (JMCC) #2

: RD
    50 850 RAND.IR DUST.AR 0.3 * 80 2080 RAND.IR { ST } ST ST -0.5 0.5 RAND.IR ST * +
    9 0 XLINE.KR 0.1 RESONZ -1 1 RAND.IR 1 PAN2
;
RD 5 2 9 INF OVERLAP-TEXTURE

\ POLICE STATE (JMCC) #2

: ND
    0.02 0.12 RAND.IR 0 6.28319 RAND.IR SINOSC.KR
    0 600 RAND.IR * 700 1300 RAND.IR + 0 SINOSC.AR
    80 120 RAND.IR LFNOISE2.AR * 0.1 * -1 1 RAND.IR 1 PAN2
;
ND 4 CLONE MIX 0.4 LFNOISE2.KR 90 * 620 + LFNOISE2.AR
0.3 LFNOISE2.KR 0.15 * 0.18 + * + 0.3 0.3 3 COMBL
PLAY

\ UPLINK (JMCC) #2

: P0 0 20 RAND.IR 0 0 1 RAND.IR LFPULSE.KR ;
: P1 0 4 RAND.IR 0 0 1 RAND.IR LFPULSE.KR 0 8000 RAND.IR * 0 2000 RAND.IR + ;
: UPLINK P0 P1 * 2 CLONE MIX 0 0.5 LFPULSE.AR 0.04 * -0.8 0.8 RAND.IR 1 PAN2 ;
UPLINK 4 1 5 INF OVERLAP-TEXTURE

\ DATA SPACE (JMCC) #2

0 200 RAND.IR 0 0 1 RAND.IR LFPULSE.KR
0 40 RAND.IR 0 0 1 RAND.IR LFPULSE.KR
0 8000 RAND.IR * 0 2000 RAND.IR + * 0 20 RAND.IR 0 0 1 RAND.IR LFPULSE.KR
0 4 RAND.IR 0 0 1 RAND.IR LFPULSE.KR
0 8000 RAND.IR * 0 2000 RAND.IR + * + 0 20 RAND.IR 0 0 1 RAND.IR LFPULSE.KR
0 4 RAND.IR 0 0 1 RAND.IR LFPULSE.KR
0 8000 RAND.IR * 0 2000 RAND.IR + * + 0 0.5 LFPULSE.AR
0.04 * 0 3 RAND.IR LFNOISE0.KR
0.8 * 1 PAN2.AR
0.15 0.35 RAND.IR 0.15 0.35 RAND.IR 3 COMBL
1 6 4 INF OVERLAP-TEXTURE

\ CYMBALISM (JMCC) #2

: N 15 ;
: F1 500 2500 RAND.IR ;
: F2 0 8000 RAND.IR ;
: PARAM N 0 DO F1 F1 F2 + RAND.IR 1 1 5 RAND.IR LOOP N 3 * MCE ;
0.5 3.5 RAND.IR 0 IMPULSE.AR 0.004 DECAY WHITENOISE.AR 0.03 * * 1 0 1 PARAM KLANK.AR 2 CLONE
3 6 6 INF OVERLAP-TEXTURE

\ CYMBALISM ACCELERANDO (JMCC) #2

: N 15 ;
: F1 500 2500 RAND.IR ;
: F2 0 8000 RAND.IR ;
: PARAM N 0 DO F1 F1 F2 + RAND.IR 1 1 5 RAND.IR LOOP N 3 * MCE ;
: TF 0.5 4.5 0 LINRAND.IR 0.5 35.5 RAND.IR 12 0 XLINE.KR ;
TF 0 IMPULSE.AR 0.004 DECAY WHITENOISE.AR 0.02 * * 1 0 1 PARAM KLANK.AR 2 CLONE
4 4 INF XFADE-TEXTURE

\ RING MODULATED KLANK (JMCC) #2

: P 8 ;
: PARAM P 0 DO 100 10000 RAND.IR 1 0.2 1 RAND.IR LOOP P 3 * MCE ;
0.1 0.4 RAND.IR LFNOISE2.KR 200 * 350 400 RAND.IR + 0 SINOSC.AR 20 DUST.AR 0.02 *
1 0 1 PARAM KLANK.AR * -1 1 RAND.IR 1 PAN2
4 4 4 INF OVERLAP-TEXTURE

\ ANALOGUE DAZE (JMCC) #3

: TWO-PI 2 PI * ;
: REV UNMCE SWAP 2 MCE ;
: SEQU { S TR } TR 0 INF S DSEQ.DR DEMAND.KR ;
: NT-SEQ 55 63 60 63 57 65 62 65 8 MCE ;
: FREQ { OCT CLK-RT PWM-RT FLT-RT }
    CLK-RT 0 IMPULSE.KR { TR }
    NT-SEQ OCT 12 * + MIDICPS TR SEQU { SQ }
    PWM-RT 0 TWO-PI RAND.IR SINOSC.KR 0.4 * 0.5 + { PWM }
    FLT-RT 0 TWO-PI RAND.IR SINOSC.KR 1400 * 2000 + { CF }
    SQ 0.05 LAG 0 PWM LFPULSE.AR 0.1 * CF 1 15 / RLPF ;
: NOISE 0.3 LFNOISE1.KR 6000 * 8000 + LFNOISE0.AR 0.07 0.08 2 MCE * ;
: PULSES 2 0 IMPULSE.AR 0.15 DECAY NOISE * ;
: PLAIN 1 8 0.31 0.2 FREQ 0 2 0.13 0.11 FREQ 2 MCE PULSES + ;
: ECHOED PLAIN 0.375 0.375 5 COMBN PLAIN REV + 0.4 * ;
: ENV 1 1 0 1 REMOVE-SYNTH 2 56 2 1 ENV-LINEN ENVGEN.KR ;
ECHOED ENV * PLAY

\ SYNTHETIC PIANO (JMCC) #3

: N 36 90 IRAND.IR ;
: F 0.1 0.5 RAND.IR ;
: PH 0 6.28319 RAND.IR ;
: C { O }
    3000 LFNOISE2.AR F PH IMPULSE.AR
    0.1 * 0.008 0.04 DECAY2 * 1 N O + MIDICPS / 1 N -0.05 + MIDICPS /
    6 COMBL ;
: SP -0.05 C 0 C + 0.04 C + N 36 - 27 / 1 - 1 PAN2 ;
SP 6 0 6 INF OVERLAP-TEXTURE

\ REVERBERATED SINE PERCUSSION (JMCC) #3

: RSP { D C A }
  D 0 DO 2 D / DUST.AR 50 * 200 0 3000 RAND.IR + 0.003 RESONZ LOOP D MCE MIX { S }
  S 0.048 0.48 DELAYN 0.1 C 0 DO 0 0.1 RAND.IR LOOP C MCE LFNOISE1.KR
  0.04 * 0.05 + 15 COMBL MIX
  A 0 DO 0.05 0 0.05 RAND.IR 2 CLONE 1 ALLPASSN LOOP 0.2 * S + ;
6 5 4 RSP
10 10 2 INF OVERLAP-TEXTURE

\ REVERBERATED NOISE BURSTS (JMCC) #3

: S 0.6 DUST.AR 0.2 * 0.15 DECAY PINKNOISE.AR * 0.6 DUST.AR 0.2 * 0.15 DECAY PINKNOISE.AR * ;
: Z 0.048 0.048 DELAYN ;
: Y 0.1 0 0.1 RAND.IR 6 CLONE LFNOISE1.KR 0.04 * 0.05 + 15 COMBL MIX ;
: F 4 0 DO 0.05 0 0.05 RAND.IR 2 CLONE 1 ALLPASSN LOOP ;
S Y F + PLAY

\ ANALOG BUBBLES WITH MOUSE CONTROL (JMCC) #3

2 40 1 0.2 MOUSEX.KR ( LFO 2 RATE )
0 LFSAW.KR -3 * 80 + ( LFO 2 = DEPTH & OFFSET IN SEMITONES )
0.1 10 1 0.2 MOUSEY.KR ( LFO 1 RATE )
0 LFSAW.KR 24 * + ( DEPTH IN SEMITONES, OFFSET IS LFO_2 )
MIDICPS ( CONVERT TO FREQUENCY )
0 SINOSC.AR 0.04 *
0.2 0.2 4 COMBN ( ECHOING SINE WAVE )
PLAY

\ BERLIN 1977 (JMCC) #4

: SEQU { SEQ TR } TR 0 INF SEQ DSEQ.DR DEMAND.KR ;
: SEQUR { SEQ TR } TR 0 INF SEQ DSHUF.DR DEMAND.KR ;
: HALF-PI PI 0.5 * ;
: CLOCK_RATE 5 20 1 0.2 MOUSEX.KR ;
: CLOCK_TIME 1 CLOCK_RATE / ;
: CLOCK CLOCK_RATE 0 IMPULSE.KR ;
: CLOCK_16 CLOCK 16 0 PULSEDIVIDER ;
: PATTERN 55 60 63 62 60 67 63 58 8 MCE ;
: NOTE PATTERN CLOCK SEQU ;
: FREQ -12 -7 -5 0 2 5 6 MCE CLOCK_16 SEQUR NOTE + MIDICPS ;
: ENV CLOCK 0.05 CLOCK_TIME * 2 CLOCK_TIME * DECAY2 ;
: AMP ENV 0.1 * 0.02 + ;
: FILT ENV 0.17 0 FSINOSC.KR 800 * * 1400 + ;
: PW  0.08 HALF-PI SINOSC.KR 0.45 * 0.5 + ;
: SIG FREQ PW PULSE.AR AMP * ;
: BELIN-1977 SIG FILT 0.15 RLPF 0.2 0.2 0.17 2 MCE 1.5 COMBN ;
BELIN-1977 PLAY

\ METAL PLATE (JMCC) #4

: TAP { B DT NC } B 1 0 DT SR NEG * 1 0 NC PLAYBUF.AR ;
: SR 48000 ;
: N 4 ;
: MAXDT SR 0.03 * ;
: BUF MAXDT 0 DO 0 LOOP MAXDT MCE AS-LOCAL-BUF ;
: TAP-TM 0.015 0.03 RAND.IR ;
: EXC-FREQ 10 8000 0 0.2 MOUSEY.KR ;
: EXC-TRIG 0.5 0 IMPULSE.AR 0.2 * ;
: EXC EXC-TRIG 0.01 0.2 DECAY2 EXC-FREQ LFNOISE2.AR * ;
: FLT-FREQ 10 5000 0 0.2 MOUSEX.KR ;
: ND
    BUF { B } B TAP-TM 1 TAP FLT-FREQ LPF 0.98 * { FLT }
    FLT B 0 1 0 1 1 1 0 FLT EXC + RECORDBUF.AR 2 MRG
;
: MP 0 N 0 DO ND + LOOP ;
MP PLAY

\ SAMPLE AND HOLD LIQUIDITIES (JMCC) #4

: R 1 200 1 0.1 MOUSEX.KR ;
: T R RECIP ;
: C R 0 IMPULSE.KR 0.4 * ;
: CF 100 8000 1 0.1 MOUSEY.KR ;
: F WHITENOISE.KR CF * 0.5 * CF + C LATCH ;
: P WHITENOISE.KR C LATCH ;
F 0 SINOSC.AR C T 0.1 * T 0.9 * DECAY2 * P 1 PAN2 0.3 0.3 2 COMBN PLAY

\ RANDOM PANNING SINES (JMCC) #4

80 2000 0 LINRAND.IR 0 FSINOSC.AR
0.8 1.2 RAND.IR LFNOISE1.KR 0.82 0.98 RAND.IR LFNOISE1.KR PAN2
8 CLONE MIX 0.05 * 8 8 2 INF OVERLAP-TEXTURE

\ DISTORT INPUT (JMCC) #5

0 1 2 MCE SOUNDIN 1 100 1 0.2 MOUSEX.KR * DISTORT 0.4 * PLAY

\ RING MODULATE INPUT (JMCC) #5

0 1 2 MCE SOUNDIN 10 4000 1 0.2 MOUSEX.KR 0 SINOSC.AR * PLAY

\ FILTER INPUT (JMCC) #5

: RQ 0.01 1 1 0.2 MOUSEY.KR ;
: CF 100 12000 1 0.2 MOUSEX.KR ;
0 1 2 MCE SOUNDIN 0.4 * RQ SQRT * CF RQ RLPF PLAY

\ SWEEPY NOISE (JMCC) #6

: X 4 60 1 0.1 MOUSEX.KR ;
: Y 200 8000 1 0.1 MOUSEY.KR ;
: F WHITENOISE.AR 2 CLONE 0.03 * X 0 LFSAW.KR Y * Y 1.2 * + 0.1 RLPF ;
F 0.3 0.3 2 COMBN F + PLAY

\ STRING WANDER-CLUSTER (JMCC) #6

: T 6 0 DO 0.15 I 6 / IMPULSE.KR LOOP 6 MCE ;
: G 6 0 DO 0.15 I 6 / 0.5 LFPULSE.KR LOOP 6 MCE ;
: E G 1 0 1 DO-NOTHING 1.3 0.1 1.3 ENV-SIN ENV-ASR ENVGEN.KR ;
: D 1 50 100 T TRAND.KR 0 15 T TRAND.KR + 7 - 50 120 FOLD MIDICPS / ;
: SW WHITENOISE.AR 0.008 * 0.01 D D 1000 * COMBC -1 1 T TRAND.KR E PAN2 MIX ;
SW PLAY

\ COMB DELAY SWEEPS (JMCC) #6

: T 6 0 DO 0.15 I 6 / IMPULSE.KR LOOP 6 MCE ;
: G 6 0 DO 0.15 I 6 / 0.5 LFPULSE.KR LOOP 6 MCE ;
: E G 1 0 1 DO-NOTHING 1.3 0.1 1.3 ENV-SIN ENV-ASR ENVGEN.KR ;
: D -7 8 T TRAND.IR 50 120 FOLD ;
: W WHITENOISE.AR 0.005 * 0.01 ;
: CDS W 1 D D 4 0 LINE.KR MIDICPS / 1 D MIDICPS / 1000 * COMBC -1 1 T TRAND.IR E PAN2 ;
CDS MIX PLAY

\ NOISE BURST SWEEP (JMCC) #6

: CF 400 8000 1 0.2 MOUSEY.KR ;
WHITENOISE.AR 2 CLONE
0 -1 1 RAND.IR 10 60 1 0.2 MOUSEX.KR +
-1 LFSAW.KR MAX * 0.2 0 SINOSC.KR CF * 1.05 CF * + 0.1 RESONZ
4 2 4 INF OVERLAP-TEXTURE

\ SAUCER BASE (JMCC) #6

: R1 0 SWAP RAND.IR ;
: RLOC -1 1 RAND.IR 1 PAN2 ;
: OSC SINOSC.AR ;
: DRMS DUP ROT * SWAP 1.1 * + ;
: SB 20 R1 0 OSC 1000 R1 DRMS 0 OSC 5000 R1 DRMS 0 OSC 0.1 * RLOC ;
SB 2 6 4 INF OVERLAP-TEXTURE

\ ALIEN MEADOW (JMCC) #6

: ALIEN-MEADOW
  0 20 RAND.IR { A }
  0 5000 RAND.IR { B }
  0 20 RAND.IR { C }
  -1 1 RAND.IR { P }
  A 0 SINOSC.AR B * 0.1 * B + { F }
  F 0 SINOSC.AR P C 0 SINOSC.AR 0.05 * 0.05 + PAN2.AR ;
: ALIEN-MEADOWS ALIEN-MEADOW 2 6 6 32 OVERLAP-TEXTURE ;
ALIEN-MEADOWS

(
20 RAND_ 0 SINOSC.AR 5000 RAND_ * 0.1 * 5000 RAND_ + 0 SINOSC.AR
-1 1 RAND.IR 20 RAND_ 0 SINOSC.AR 0.05 * 0.05 + PAN2 DRAW
)

\ BIRDIES (JMCC) #6

0.4 0 1 RAND.IR + 0 0 0.8 RAND.IR 0.1 + LFPULSE.KR
0 3 RAND.IR 4 + * 2 + 0.4 0 1 RAND.IR + 0 0 0.8 RAND.IR 0.1 + LFPULSE.KR
0 3 RAND.IR 4 + * + 0 LFSAW.KR
1000 0 800 RAND.IR + NEG * 4000 -1200 1200 RAND.IR + + 0.05 LAG 0 SINOSC.AR
0.2 0 0.5 RAND.IR + 0 0.4 LFPULSE.KR
0.02 * 0.3 LAG * -1 1 RAND.IR 1 PAN2
7 4 4 INF OVERLAP-TEXTURE

\ PHASE MODULATION WITH SLOW BEATS (JMCC) #6

: X 100 6000 1 0.2 MOUSEX.KR ;
: Y 0 2 0 0.2 MOUSEY.KR ;
: F 0 X RAND.IR DUP -1 1 RAND.IR + 2 MCE ;
: O 0 3 0 DO F 0 FSINOSC.AR Y * + LOOP ;
: PM F O SINOSC.AR 0.1 * ;
PM 4 4 4 INF OVERLAP-TEXTURE

\ HARD SYNC SAWTOOTH WITH LFO (JMCC) #6

: HSSWL
    0 50 RAND.IR 30 + MIDICPS { F }
    0.2 0 0 PI 2 * RAND.IR 2 MCE SINOSC.KR 2 F * * 3 F * + { O }
    F F 0.2 + 2 MCE O SYNCSAW.AR 0.05 * ;
: REV UNMCE SWAP 2 MCE ;
: CMB { Z } Z 0.3 0.3 4 COMBN.AR Z REV + ;
' CMB TEXTURE-POST-PROC
HSSWL 4 4 4 INF OVERLAP-TEXTURE

\ NOISE MODULATED SINES (JMCC) #6

: F 60 100 RAND.IR MIDICPS ;
: NMS F F 0.2 + 2 MCE 0 FSINOSC.AR F 0.15 0.16 2 MCE * LFNOISE2.KR 0.1 * * ;
: PP { Z } Z 0.3 0.3 4 COMBN.AR Z UNMCE SWAP 2 MCE + ;
' PP TEXTURE-POST-PROC
NMS 4 4 4 INF OVERLAP-TEXTURE

\ NOISE MODULATED SAWTOOTHS (JMCC) #6

: F 60 100 RAND.IR MIDICPS ;
: NMS F F 0.2 + 2 MCE 0 LFSAW.AR F 0.15 0.16 2 MCE * LFNOISE2.KR 0.1 * * ;
: PP { Z } Z 0.3 0.3 4 COMBN.AR Z UNMCE SWAP 2 MCE + ;
' PP TEXTURE-POST-PROC
NMS 4 4 4 INF OVERLAP-TEXTURE

\ ALEATORIC QUARTET (JMCC) #7

: AMP 0.07 ;
: BASE_MN 66 ;
: DMUL 0.01 1 0 0.1 MOUSEX.KR RECIP 0.5 * AMP * ;
: MK_F 1 0.5 0.25 3 MCE CHOOSE LFNOISE0.KR 7 * BASE_MN + 30 RAND2.IR + 1 ROUND 0.2 LAG MIDICPS ;
: MK_N PINKNOISE.AR 0 8 LFNOISE1.KR DMUL * AMP DMUL - + MAX * ;
: MK_S MK_N 0.02 MK_F RECIP 3 COMBL 1 RAND2.IR 1 PAN2 ;
: RAPF 0.05 0 0.05 RAND.IR 2 CLONE 1 ALLPASSN ;
: AQ MK_S 4 CLONE MIX 5 0 DO RAPF LOOP 0.995 LEAKDC ;
AQ PLAY

\ SLOW BEATING SINES (JMCC) #7

( STORE UID, FIX UID OF CENTRAL FREQ, RESTORE UID )

: N 10 ;
: TWO-PI 2 PI * ;
: PH 0 TWO-PI RAND.IR ;
: DEV -5 5 RAND.IR + 1 PH ;
: DEV-3 { F } 1 0 F 1 PH 2 0 DO F DEV LOOP ;
: FREQ N 0 DO GET-UID I SET-UID 24 60 RAND.IR MIDICPS SWAP SET-UID LOOP ;
: OSC FREQ 0 N 0 DO SWAP DEV-3 9 MCE KLANG.AR + LOOP ;
OSC OSC 2 MCE 0.1 N / *
4 4 3 INF OVERLAP-TEXTURE

\ SLOW BEATING HARMONIC SINES (JMCC) #7

: N 8 ; ( NOTES )
: M 5 ; ( HARMONICS )
: D 0.4 ; ( FREQUENCY DEVIATION )
: RFREQ 0 2 4 5 7 9 6 MCE CHOOSE 0 7 RAND.IR 12 * + 0 12 RAND.IR + 24 + MIDICPS ;
: ROSC { F } F D RAND2.IR + 0 PI RAND.IR SINOSC.AR 2 CLONE ;
: RHARM { F } 0 M 0 DO F I * ROSC + LOOP ;
: SBHS 0 N 0 DO RFREQ RHARM + LOOP 0.1 N / * ;
SBHS 3 6 3 INF OVERLAP-TEXTURE

\ TAPPING TOOLS (JMCC) #7

: PARAM 4 0 DO 400 8400 RAND.IR 1 0.01 0.11 RAND.IR LOOP 12 MCE ;
: TT
    1 21 0 LINRAND.IR 64 0.125 60 0 XLINE.KR * 0 IMPULSE.AR
    0.03 * 0.001 DECAY 1 0 1 PARAM KLANK
    -1 1 RAND.IR 1 1 0 1 2 1 4 1 1 ENV-LINEN ENVGEN.KR PAN2
;
: RAPF 3 0 DO 0.05 0 0.05 RAND.IR 2 CLONE 2 ALLPASSN LOOP ;
' RAPF TEXTURE-POST-PROC
TT 2 1 3 INF OVERLAP-TEXTURE

\ MODAL SPACE (JMCC) #8

: B 0 2 3.2 5 7 9 10 7 MCE AS-LOCAL-BUF ;
: X 0 14 LINEAR 0.1 MOUSEX.KR ;
: K B X 12 DEGREETOKEY.KR ;
: C { N R }
    R K + N 0.04 * + MIDICPS 0 SINOSC.AR 0.1 * { O }
    48 55 2 MCE MIDICPS 0 0.15 LFPULSE.AR { T }
    0.1 0 SINOSC.AR 10 * R + MIDICPS { F }
    T F 0.1 RLPF.AR 0.1 * { D }
    O D + { M }
    M 0.31 0.31 2 COMBN.AR M + ;
: N 3 3 2 MCE LFNOISE1.KR ;
: MODAL-SPACE N 48 C N 72 C + 0.2 * ;
MODAL-SPACE PLAY

\ LANDON ROSE (JMCC) #8

: F MIDICPS 1 3 ;
: PARAM
    32 F 43 F 54 F  89 F 12 MCE
    10 F 34 F 80 F 120 F 12 MCE
    67 F 88 F 90 F 100 F 12 MCE
    76 F 88 F 99 F 124 F 12 MCE
;
: K { P Z E } PINKNOISE.AR 2 CLONE 0.001 * E * 1 0 1 P KLANK.AR Z + ;
: LR 4 0 DO 0.125 I 0.5 * PI * SINOSC.AR ABS K LOOP ;
PARAM 0 LR PLAY

\ DEEP TRIP (JMCC) #9

: F 0 0.3 RAND.IR LFNOISE1.KR 60 * 70 + MIDICPS ;
: S F 0 SINOSC.AR F 0 0.5 RAND.IR * LFNOISE2.AR 0 0 8 RAND.IR LFNOISE1.KR
    0 40 RAND.IR 0 SINOSC.KR 0.1 * * MAX * * 0 5 RAND.IR LFNOISE1.KR 1 PAN2 ;
: C S 0.5 0.3 0.5 RAND.IR 2 CLONE 20 COMBN ;
S C + C + 4 12 4 INF OVERLAP-TEXTURE

\ SAWED CYMBALS (JMCC) #9

: R2 RAND.IR ;
: PARAM 15 0 DO 500 2500 R2 DUP 0 8000 R2 + R2 1 2 6 R2 LOOP 45 MCE ;
: EXC 0 600 R2 0 600 R2 12 DO-NOTHING XLINE.KR ;
: CYM EXC 0 LFSAW.AR 0.0005 * 1 0 1 PARAM KLANK ;
CYM 4 4 6 INF OVERLAP-TEXTURE

\ SIDEREAL TIME (JMCC) #9

: P 15 ;
: Z P 0 DO 100 6000 EXPRAND.IR 1 2 6 RAND.IR LOOP P 3 * MCE ;
: F 40 300 EXPRAND.IR 40 300 EXPRAND.IR 12 0 XLINE.KR ;
: T F 0 0.1 0.9 RAND.IR LFPULSE.AR 0.002 * 0 0 8 RAND.IR LFNOISE2.KR MAX * ;
: REV UNMCE SWAP 2 MCE ;
: CMB { Z } Z 0.6 0.1 0.6 RAND.IR 8 COMBN Z REV + ;
' CMB TEXTURE-POST-PROC
T 1 0 1 Z KLANK 2 CLONE DISTORT 0.1 *
4 4 6 INF OVERLAP-TEXTURE

\ CONTAMINATION ZONE (JMCC) #9

: PARAM 4 0 DO 50 2000 RAND.IR 1 0.2 4 RAND.IR LOOP 12 MCE ;
: CZ
    800 8000 EXPRAND.IR { F }
    PINKNOISE.AR 0 3 RAND.IR LFNOISE1.KR 0.0008 * 0.0022 + *
    1 0 1 PARAM KLANK.AR ABS -1 1 2 MCE CHOOSE *
    0 1 0 LINRAND.IR 0 SINOSC.KR 0.7 F * * F + 0.1 RLPF
    0 1 RAND.IR LFNOISE1.KR 0 150 0 LINRAND.IR 0 0.2 0.4 RAND.IR LFPULSE.KR PAN2
;
: CZ-PP 6 0 DO 0.04 0 0.04 RAND.IR 2 CLONE 16 ALLPASSN LOOP ;
' CZ-PP TEXTURE-POST-PROC
CZ 3 8 4 INF OVERLAP-TEXTURE

\ STRUMMABLE GUITAR (JMCC) #11

: SCALE 52 57 62 67 71 76 ;
: STR { SC IX }
    0 1 LINEAR 0.2 MOUSEX.KR { X }
    X 0.25 IX 0.1 * + > HPZ1.KR ABS { T }
    T 0.05 DECAY.KR PINKNOISE.AR * { N }
    SC MIDICPS RECIP { DT }
    N DT DT 4 COMBL.AR { S }
    S IX 0.2 * 0.5 - 1 PAN2.AR ;
: STRS SCALE 0 6 0 DO SWAP I STR + LOOP ;
: GTR STRS 12000 LPF.AR 0.995 LEAKDC.AR ;
GTR PLAY

\ DRONE PLUS RHYTHM (JMCC) #12

: SCALE 0 2 3 5 7 9 10 7 MCE ;
: ISEQR { S TR } TR TR 0 INF S DXRAND.DR DEMAND.AR * ;
: DRONE-1
    24 36 2 MCE CHOOSE 0.08 RAND2.IR + MIDICPS { F0 }
    F0 F0 0.2 + 2 MCE 0 LFSAW.AR F0 0.05 0.04 2 MCE * LFNOISE2.KR * 0.06 *
    1000 3000 RAND.IR LPF
;
: DRONE-2
    60 72 2 MCE CHOOSE SCALE CHOOSE + 0.05 RAND2.IR 2 CLONE + MIDICPS
    0 SINOSC.AR 0.04 0.07 RAND.IR * 0 1 RAND.IR 0.8 > *
;
: RHY
    48 60 72 84 4 MCE CHOOSE SCALE CHOOSE + 0.03 RAND2.IR + { M }
    0 1 0 1 1 0 6 MCE 1.5 3 6 3 MCE CHOOSE 0 IMPULSE.AR ISEQR { SQ }
    M MIDICPS 0 0.4 LFPULSE.AR 0.03 0.08 RAND.IR * { SG }
    SQ 0.004 0.2 0.7 RAND.IR DECAY2 SG * 800 2000 EXPRAND 0.1 RLPF
;
: REV-CMB { Z } Z 0.5 0.5 6 COMBN Z UNMCE SWAP 2 MCE + ;
: DRONE-1-TXT DRONE-1 4 4 8 INF OVERLAP-TEXTURE ;
: DRONE-2-TXT DRONE-2 4 6 3 INF OVERLAP-TEXTURE ;
: RHY-TXT RHY 6 6 6 INF OVERLAP-TEXTURE ;
' REV-CMB TEXTURE-POST-PROC
FORK DRONE-1-TXT .
FORK DRONE-2-TXT .
FORK RHY-TXT .
( KILLALL )

\ BLIPS 001 (JMCC) #SC3D1.5

: DUP' 2 CLONE UNMCE ;
: BLIPS
    0 1 RAND.IR 0.8 <
    0.25 400 EXPRAND.IR 0.25 400 EXPRAND.IR 4 0 XLINE.KR
    2 100 EXPRAND.IR 2 100 EXPRAND.IR 4 0 XLINE.KR BLIP.AR DUP' *
    -1 1 RAND.IR -1 1 RAND.IR 4 0 LINE.KR 0.3 PAN2.AR * ;
: APF DISTORT 6 0 DO 0.05 0 0.05 RAND.IR 4 ALLPASSN LOOP ;
' APF TEXTURE-POST-PROC
BLIPS 2 1 12 INF OVERLAP-TEXTURE

( BLIPS APF DRAW )

\ ZIZLE (JMCC) #SC3D1.5

: TWO-PI PI 2 * ;
: A 0.7 1.3 RAND.IR 1 2 MCE * 0 TWO-PI RAND.IR 2 CLONE SINOSC.AR 0.1 * MIX ;
0.3 8 EXPRAND.IR A 0 MAX
6 24 EXPRAND.IR A ABS *
24 108 RAND.IR MIDICPS 0 TWO-PI RAND.IR SINOSC.AR *
-1 1 RAND.IR 1 PAN2.AR
4 4 12 INF OVERLAP-TEXTURE

\ BABBLING BROOK (JMCC) #SC3

: B { F M A G } BROWNNOISE.AR 0.99 ONEPOLE BROWNNOISE.AR F LPF M * A + 0.03 RHPF G * ;
14 400 500 0.006 B 2 CLONE 20 800 1000 0.01 B 2 CLONE + PLAY

\ MRIDANGAM (JMCC) #SPE3

: DRUM { A } WHITENOISE.AR 70 * A 0.002 0.1 DECAY2 * 261.62557 0.02 RESONZ 4 * DISTORT 0.4 * ;
: DRONE 60 60.04 2 MCE MIDICPS SAW.AR 67 67.04 2 MCE MIDICPS SAW.AR + 4186.00904 LPF 0.007 * ;
: TEN 10 MCE DSEQ.DR ;
: A_SOLO
    30
    1 0.9 0 0 0.7 0 0.2 0 0.7 0 0 TEN
    1 0.9 0.2 0 0.7 0 0.2 0 0.7 0 0 TEN
    1 0.9 0 0 0.7 0 0.2 0 0.7 0 0.2 TEN
    1 0.9 0 0 0.7 0.2 0.2 0 0.7 0 0 TEN
    1 0.9 0 0 0.7 0 0.2 0.2 0.7 0.2 0 TEN
    1 0.9 0.2 0.2 0.7 0.2 0.2 0.2 0.7 0.2 0.2 TEN
    1 0.9 0.2 0.2 0.7 0.2 0.2 0.2 0.7 0 0 TEN
    1 0.9 0 0 0.7 0.2 0.2 0.2 0.7 0 0 TEN
    1 0.9 0 0.4 0 0.4 0 0.4 0 0.4 0 TEN
    1 0.9 0 0 0.4 0 0 0.4 0.2 0.4 0.2 TEN
    1 0.9 0 0.2 0.7 0 0.2 0 0.7 0 0 TEN
    1 0.9 0 0 0.7 0 0 0 0.7 0 0 TEN
    1 0.9 0.7 0.7 0 0 0.2 0.2 0.2 0 0 TEN
    1 0.9 0 0 0 0 0 0 0 0 0 TEN
    14 MCE DRAND.DR
;
: A_SEQ
    1
    10 0 DSEQ.DR
    2 0.9 0 0 0 0 0 0 0 0 0 TEN
    2 0.9 0 0 0.2 0 0 0 0.2 0 0 TEN
    2 0.9 0 0 0.2 0 0.2 0 0.2 0 0 TEN
    2 0.9 0 0 0.2 0 0 0 0.2 0 0.2 TEN
    A_SOLO
    3 2 0 0.2 0.5 0 0.2 0.9 1.5 0 0.2 0.5 0 0.2 0.9 1.5 0 0.2 0.5 0 0.2 20 MCE DSEQ.DR
    1 5 DSEQ.DR
    8 MCE DSEQ.DR
;
1 8 / 0 DO-NOTHING 1 A_SEQ DSEQ.DR 0 TDUTY.AR DRUM DRONE + PLAY

( 1 DUST.AR DRUM DRONE + PLAY )
